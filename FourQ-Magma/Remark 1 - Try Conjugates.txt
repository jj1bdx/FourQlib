/*************************************************************************************
* FourQ: 4-dimensional decomposition on a Q-curve with CM in twisted Edwards form
*
*    Copyright (c) Microsoft Corporation. All rights reserved.
*
*
* Abstract: MAGMA script used to illustrate Remark 1, i.e., that different choices of 
*			roots and/or Galois conjugates give rise to square d's, thereby justifying
*			the use of an isogeny to obtain a non-square d. Only 4 different d's arise
*			and they are all square. 
*
* This code is based on the paper "FourQ: four-dimensional decompositions on a 
* Q-curve over the Mersenne prime" by Craig Costello and Patrick Longa, in Advances 
* in Cryptology - ASIACRYPT, 2015.
* Preprint available at http://eprint.iacr.org/2015/565.
**************************************************************************************/  

clear;

different_ds:={};

p:=2^127-1;
Fp:=GF(p);
Fp2<i>:=ExtensionField<Fp,x|x^2+1>;

N:=28948022309329048855892746252171976962840845737041638204827285762769800380856;

for rt5 in AllRoots(Fp2!5,2) do
for s in [-4/9,4/9] do
	C:=9*(1+s*rt5);
	A:=2*(C-24);
	B:=-8*(C-16);
	for Bm in AllRoots(2*C,2) do
	for gal_conj in [1,p] do
		"		";
		"  TRY	";
		"		";
		Am:=12/Bm;
		Am:=Am^gal_conj;
		BBm:=Bm^gal_conj;
		"a/-1 is square?: ", IsSquare(Fp2!(12+2*Bm));
		if IsSquare(-Fp2!(12+2*BBm)) then
			d:=-(12-2*BBm)/(12+2*BBm);
			AS<x,y>:=AffineSpace(Fp2,2);
			C:=Curve(AS,[-x^2+y^2-(1+d*x^2*y^2)]);
			E:=EllipticCurve(ProjectiveClosure(C),C![0,1]);
			"group order eq N?: ", N*Random(E) eq Identity(E);
			"d is square?: ", IsSquare(d);
			Include(~different_ds,d);
		end if;
	end for;
	end for;
end for;
end for;

different_ds;

[IsSquare(d): d in different_ds];

