/*************************************************************************************
* FourQ: 4-dimensional decomposition on a Q-curve with CM in twisted Edwards form
*
*    Copyright (c) Microsoft Corporation. All rights reserved.
*
*
* Abstract: MAGMA script used to show correctness of original decomposition and the bounds
*			in Proposition 4.
*
* This code is based on the paper "FourQ: four-dimensional decompositions on a 
* Q-curve over the Mersenne prime" by Craig Costello and Patrick Longa, in Advances 
* in Cryptology - ASIACRYPT, 2015.
* Preprint available at http://eprint.iacr.org/2015/565.
**************************************************************************************/  

clear;

//Keep m indeterminate
ff<m>:=FunctionField(PolynomialRing(Rationals()));

//curve parameters from paper
V:=49293975489306344711751403123270296814; r:=15437785290780909242;
N:=73846995687063900142583536357581573884798075859800097461294096333596429543;
assert V mod r eq 0;
alpha:=V div r;

//This is the basis from Proposition 3

B:=Matrix(ff,4,4,
[16*(-60*alpha+13*r-10), 4*(-10*alpha-3*r+12), 4*(-15*alpha+5*r-13), -13*alpha-6*r+3,
32*(5*alpha-r), -8, 8, 2*alpha+r,
16*(80*alpha-15*r+18), 4*(18*alpha-3*r-16), 4*(-15*alpha-9*r+15), 15*alpha+8*r+3,
16*(-360*alpha+77*r+42), 4*(42*alpha+17*r+72), 4*(85*alpha-21*r-77),(-77*alpha-36*r-17)
]);

for i:=1 to 4 do
        B[1,i]:=B[1,i]/224;
        B[2,i]:=B[2,i]/8;
        B[3,i]:=B[3,i]/224;
        B[4,i]:=B[4,i]/448;
end for;

//the vector in Q^4 representing the scalar m
ans:=Matrix(ff,4,1,[m,0,0,0]);

//This is the claimed solution to Transpose(B)*solution = ans
TransBInv:=Matrix(ff,4,1,
[
540*V^3+10*r*(27*r-4)*V^2+6*r^2*(9*r^2-2*r+18)*V+r^3*(27*r+4)*(r^2-2),
1020*V^3+10*r*(47*r-8)*V^2+2*r^2*(51*r^2+26*r+102)*V+r^3*(47*r+8)*(r^2-2),
220*V^3+10*r*(11*r+16)*V^2+2*r^2*(11*r^2-46*r+22)*V+r^3*(11*r-16)*(r^2-2),
60*V^3+30*r^2*V^2+2*r^2*(3*r^2+2*r+6)*V+3*r^4*(r^2-2)
]);

TransBInv[1,1]:=TransBInv[1,1]*m/(6272*r^3*N);
TransBInv[2,1]:=TransBInv[2,1]*m/(25088*r^3*N);
TransBInv[3,1]:=TransBInv[3,1]*m/(25088*r^3*N);
TransBInv[4,1]:=TransBInv[4,1]*m/(1792*r^3*N);


//Show that it is indeed the answer
assert Transpose(B)*TransBInv eq ans;


/*
Show that the 16 corners of P(B) have all four coordinates less than 2^62 in absolute value
*/

for s1 in  [-1/2,1/2] do
    for s2 in  [-1/2,1/2] do
        for s3 in  [-1/2,1/2] do
            for s4 in  [-1/2,1/2] do

                corner:=s1*B[1]+s2*B[2]+s3*B[3]+s4*B[4];
                assert Abs(Rationals()!corner[1]) lt 2^62;
                assert Abs(Rationals()!corner[2]) lt 2^62;
                assert Abs(Rationals()!corner[3]) lt 2^62;
                assert Abs(Rationals()!corner[4]) lt 2^62;

            end for;
        end for;
    end for;
end for;

